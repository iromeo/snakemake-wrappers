# Example: Pair-ended reads
rule bismark_pe:
    input:
        fq_1="{sample}.1.fastq",
        fq_2="{sample}.2.fastq",
        genome="{genome}/{genome}.fa",
        bismark_indexes_dir="{genome}/Bisulfite_Genome",
        genomic_freq="{genome}/genomic_nucleotide_frequencies.txt"
    output:
        bam="{sample}_{genome}_pe.bam",
        report="{sample}_{genome}_PE_report.txt",
        nucleotide_stats="{sample}_{genome}_pe.nucleotide_stats.txt",
        bam_unmapped_1="{sample}_{genome}_unmapped_reads_1.fq.gz",
        bam_unmapped_2="{sample}_{genome}_unmapped_reads_2.fq.gz",
        ambiguous_1="{sample}_{genome}_ambiguous_reads_1.fq.gz",
        ambiguous_2="{sample}_{genome}_ambiguous_reads_2.fq.gz",
    params:
        # optional params string, e.g: -L32 -N0 -X400 --gzip
        # Useful options to tune:
        # (for bowtie2)
        # -N: The maximum number of mismatches permitted in the "seed", i.e. the first L base pairs
        # of the read (deafault: 1)
        # -L: The "seed length" (deafault: 28)
        # -I: The minimum insert size for valid paired-end alignments. ~ min fragment size filter (for
        # PE reads)
        # -X: The maximum insert size for valid paired-end alignments. ~ max fragment size filter (for
        # PE reads)
        # --gzip: Gzip intermediate fastq files
        # --ambiguous --unmapped
        # -p: bowtie2 parallel execution
        # --multicore: bismark parallel execution
        # --temp_dir: tmp dir for intermediate files instead of output directory
        extra=' --ambiguous --unmapped --nucleotide_coverage',
        basename='{sample}_{genome}'
    wrapper:
        "master/bio/bismark/bismark"

# Example: Single-ended reads
rule bismark_se:
    input:
        fq="{sample}.fq.gz",
        genome="{genome}/{genome}.fa",
        bismark_indexes_dir="{genome}/Bisulfite_Genome",
        genomic_freq="{genome}/genomic_nucleotide_frequencies.txt"
    output:
        bam="{sample}_{genome}.bam",
        report="{sample}_{genome}_SE_report.txt",
        nucleotide_stats="{sample}_{genome}.nucleotide_stats.txt",
        bam_unmapped="{sample}_{genome}_unmapped_reads.fq.gz",
        ambiguous="{sample}_{genome}_ambiguous_reads.fq.gz",
    params:
        # optional params string
        extra=' --ambiguous --unmapped --nucleotide_coverage',
        basename='{sample}_{genome}'
    wrapper:
        "master/bio/bismark/bismark"
